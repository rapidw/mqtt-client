plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id "signing"
    id 'net.researchgate.release' version '2.8.1'
    id 'se.patrikerdes.use-latest-versions' version '0.2.13'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id "com.github.hierynomus.license" version "0.15.0"
//    id "com.star-zero.gradle.githook" version "1.2.0"
}

group = 'io.rapidw.mqtt'
description = "Rapidw MQTT client"

ext {
    url = 'https://github.com/rapidw/mqtt-client'
    vcsUrl = "${url}.git"
}

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    api 'io.rapidw.mqtt:rapidw-mqtt-codec:0.0.13'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'io.netty:netty-handler:4.1.45.Final'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'org.mockito:mockito-core:3.3.0'
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"

    intTestImplementation 'ch.qos.logback:logback-classic:1.2.3'
    intTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    intTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
    intTestImplementation "commons-io:commons-io:2.6"
    intTestImplementation 'commons-codec:commons-codec:1.15'
    intTestCompileOnly 'org.projectlombok:lombok:1.18.12'
    intTestAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
    intTestImplementation 'commons-codec:commons-codec:1.15'
}

license {
    header project.file("gradle/license-header")
}

tasks.withType(JavaCompile) {

    options.encoding = "UTF-8"
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

javadoc {
    options {
        encoding = 'UTF-8'
        locale 'en_US'
        jFlags '-Duser.language=en_US'
    }

    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
}
java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "$group:${project.name}"
                description = project.description
                url = project.url

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name = 'pvtyuan'
                        email = 'pvtyuan@foxmail.com'
                    }
                }

                scm {
                    connection = "scm:git:${project.vcsUrl}"
                    developerConnection = "scm:git:${project.vcsUrl}"
                    url = project.url
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username mavenCentralUsername
                password mavenCentralPassword
            }
        }
    }
}

// include pom in jar
jar {
    into("META-INF/maven/$project.group/$project.name") {
        from generatePomFileForMavenJavaPublication
        rename ".*", "pom.xml"
    }
}

release {
    failOnUnversionedFiles = false
    pushReleaseVersionBranch = "master"
}

signing {
    sign publishing.publications
}

afterReleaseBuild.dependsOn publish
